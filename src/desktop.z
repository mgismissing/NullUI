use PackageManager                      from android
use run                                 from android
use colorama        as c
use nullui          as nui
use sys
use os

c.init!

apm = PackageManager!

with nui.Screen! as scr
    size = os.get_terminal_size!
    buf = ""
    apps = apm.list!
    while True
        scr.clear!
        scr.print(c.Fore.LIGHTWHITE_EX)
        scr.print_at(1, 1, "> "+buf+("▏" if #buf<16 else "█"))

        fapps = [,]
        if ["ls", "rb"] has buf; {fapps}<-c.Fore.LIGHTMAGENTA_EX+"Run macro";.
        for app in apps
            for part in (app as str).split(".")
                if part.startswith(buf); {fapps}<-app; break;.
            .
        .

        scr.print_at(1, 2, c.Fore.LIGHTGREEN_EX+("\r\n"+c.Fore.LIGHTWHITE_EX).join((app as str for app in fapps[:8])))

        scr.flush!
        char = sys.stdin.read(1)
        if (ord(char) between ord("a"), ord("z")) or char == "."; buf = ($+char)[:16]
        .elif char == "\x7F"; buf = $[:-1];
        .elif char == "\x03"; break;
        .elif char == "\x0D"
            match buf
                case "ls"; run("am start -a android.settings.APPLICATION_SETTINGS");.
                case "rb"; run("su -c stop && su -c start");.
                case _; if #fapps > 0; fapps[0].open!;.;.
            .
            buf = ""
        .
    .
.